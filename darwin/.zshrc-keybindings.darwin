#!/usr/bin/env zsh

################################################################################
# NOTE: ctrl-{left,right} won't be used, because they're permanently claimed
#       by OSX for for switching workspace.
################################################################################

################################################################################
# Keystrokes (bash-like)
################################################################################
backward-kill-word-bash () {
    local WORDCHARS=''
    zle backward-kill-word
}
zle -N backward-kill-word-bash

backward-kill-dir () {
    local WORDCHARS=${WORDCHARS/\/}
    zle backward-kill-word
}
zle -N backward-kill-dir

kill-word-bash () {
    local WORDCHARS=''
    zle kill-word
}
zle -N kill-word-bash

kill-word-dir () {
    local WORDCHARS=${WORDCHARS/\/}
    zle kill-word
}
zle -N kill-word-dir

backward-word-bash () {
    local WORDCHARS=''
    zle backward-word
}
zle -N backward-word-bash

backward-word-dir () {
    local WORDCHARS=${WORDCHARS/\/}
    zle backward-word
}
zle -N backward-word-dir

forward-word-bash () {
    local WORDCHARS=''
    zle forward-word
}
zle -N forward-word-bash

forward-word-dir () {
    local WORDCHARS=${WORDCHARS/\/}
    zle forward-word
}
zle -N forward-word-dir


################################################################################
# Keybindings on iterm2 (WITH 'option mac meta').
################################################################################
bindkey "^[^?"   backward-kill-word-bash   # alt-backspace
bindkey "^[w"    backward-kill-dir         # alt-w
bindkey "^[^[B"  backward-word             # alt-b
bindkey "^[^[[D" backward-word-bash        # alt-left
bindkey "^B"     backward-word-dir         # ctrl-b
bindkey "^[^[F"  forward-word              # alt-f
bindkey "^[^[[C" forward-word-bash         # alt-right
bindkey "^F"     forward-word-dir          # ctrl-f


################################################################################
# Synchronize keybindings to vscode (WITHOUT 'option meta mac').
################################################################################
if [[ ${TERM_PROGRAM} == "vscode" || ${TERM} == "xterm-kitty" ]]; then
    bindkey "∑" backward-kill-dir     # alt-w
    bindkey "∫" backward-word         # alt-b
    bindkey "ƒ" forward-word          # alt-f
    bindkey "≥" insert-last-word      # alt-.
    bindkey "∂" kill-word             # alt-d

    ###########################################################################
    # These must never run on iterm2, as they conflict with iterm2's alt-{b,f}
    ###########################################################################
    bindkey "^[b" backward-word-bash  # alt-left
    bindkey "^[f" forward-word-bash   # alt-right
    ###########################################################################
fi
